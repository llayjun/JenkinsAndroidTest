plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 29

    aaptOptions.cruncherEnabled = false
    aaptOptions.useNewCruncher = false

    defaultConfig {
        applicationId "com.llayjun.jenkinsandroidtest"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PWD
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PWD
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Jenkins"
            buildConfigField "boolean", "APP_LOG", "false"
            buildConfigField "boolean", "APP_DEBUG", "false"
        }
        debug {
            debuggable true
            signingConfig signingConfigs.release
            resValue "string", "app_name", "Jenkins测试版"
            buildConfigField "boolean", "APP_LOG", "true"
            buildConfigField "boolean", "APP_DEBUG", "true"
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.buildType.debuggable) {
                def fileName = "Mutually_${defaultConfig.versionName}-${releaseTime()}_debug.apk"
                outputFileName = fileName
            } else {
                def fileName = "hh_market_${defaultConfig.versionName}_${releaseTime()}_release.apk"
                outputFileName = fileName
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

//获取编译时间
static def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}